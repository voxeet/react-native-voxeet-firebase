
buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:7.2.2"
    }
}

apply plugin: 'com.android.library'

def ext = rootProject.ext
def _compile = ext.has("compileSdkVersion") ? ext.compileSdkVersion : 33
def _buildTools = ext.has("buildToolsVersion") ? ext.buildToolsVersion : "33.0.0"
def _minSdk = ext.has("minSdkVersion") ? ext.minSdkVersion : 21
def _targetSdk = ext.has("targetSdkVersion") ? ext.targetSdkVersion : 33

if(_minSdk < 21) _minSdk = 21

android {
    compileSdkVersion _compile
    buildToolsVersion _buildTools

    defaultConfig {
        minSdkVersion _minSdk
        targetSdkVersion _targetSdk
        versionCode 1
        versionName "1.0"
    }
    lintOptions {
        abortOnError false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

repositories {
    maven {
        // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
        url("$rootDir/../node_modules/react-native/android")
    }
    google()
    mavenCentral()
    mavenLocal()
    maven {
        url "https://android-sdk.voxeet.com/release"
        content {
            includeGroup "com.voxeet.sdk"
        }
    }
    maven {
        url "https://android-sdk.voxeet.com/beta"
        content {
            includeGroup "com.voxeet.sdk"
        }
    }
}

dependencies {
    implementation 'com.facebook.react:react-native:+'
    implementation 'io.dolby:uxkit-firebase:3.7.5'
}
  